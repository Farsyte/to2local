use { CONSOLE } from ksp::console
use { MODE_AUTOPILOT, MODE_PROGRADE } from ksp::vessel
use { min, max, sqrt } from core::math

use { Common } from fx::common

pub struct Ascent(common: Common) {
    common : Common = common

    mode : int = 0
    mode_met : float = 0.0

    alt_min : float = 0.0
    alt_max : float = 70000.0 }

impl Ascent {

    // ascent.gravityturn: guide a powered ascent APPROXIMATELY along a gravity turn
    //
    // an exact gravity turn would have us locking to surface-prograde through the
    // atmosphere and transition to orbital-prograde in space, and would require very
    // careful tuning of initial launch attitude (or an initial pitchover), coupled
    // with careful thrust design (or active thrust control), which would depend on
    // details of the vessel design.
    //
    // the generic approach taken here is to lock the attitude based on altitude,
    // keep the engines at full throttle, and allow any design where the resulting
    // angle of attack is not horribly bad.

    sync fn gravityturn(self) -> float = {
        const common = self.common
        const vessel = common.vessel
        const orbit = vessel.orbit

        const des_ap = common.launch_altitude
        const obs_ap = orbit.apoapsis.value

        // detect completion. This is not a precision rise of our apoapsis,
        // we just cut the throttle when our projected apoapsis is higher than
        // the requested initial apoapsis altitude.
        if (obs_ap > des_ap) {
            common.t_mgr.throttle = 0.0
            vessel.autopilot.enabled = true
            vessel.autopilot.mode = MODE_PROGRADE
            return 0.0 }        

        if (self.mode != 3) {
            self.mode = 3
            self.mode_met = common.met()

            CONSOLE.print_line("starting gravity turn")

            // during the first call, calibrate the initial and final altitudes
            // for the turn, assure we are full throttle, and set the autopilot
            // to point us in our desired direction.

            self.alt_min = vessel.altitude_sealevel
            self.alt_max = max(vessel.main_body.atmosphere_depth, self.alt_min + 1000)

            common.t_mgr.throttle = 1.0
            vessel.autopilot.enabled = true
            vessel.autopilot.mode = MODE_AUTOPILOT }        

        const atm_frac = (vessel.altitude_sealevel - self.alt_min) / (self.alt_max - self.alt_min)
        const atm_clip = max(0, min(1, atm_frac))
        const atm_sqrt = sqrt(atm_clip)
        const pit_frac = 1.0 - atm_sqrt

        const pitch = 90.0 * pit_frac

        const heading = common.launch_azimuth
        const direction = vessel.heading_direction(heading, pitch, 0)

        vessel.autopilot.target_orientation = direction.vector

        // this step needs to execute frequently enough
        // to make our target_orientation rotate smoothly.
        //
        // NOTE: the above could be packaged as a function
        // executed by the throttle manager. How do we feel
        // about doing all of the above faster than 10 Hz?

        0.1 }    

    // ascent.spacecoast: unpowered ascent from mid-atmosphere to space
    sync fn spacecoast(self) -> float = {
        const common = self.common
        const vessel = common.vessel

        if (self.mode != 4) {
            self.mode = 4
            self.mode_met = common.met()

            CONSOLE.print_line("coasting to space")

            common.t_mgr.throttle = 0.0
            vessel.autopilot.enabled = true

            // Hmmm. I don't see how to switch between
            // Surface Prograde and Orbital Prograde.
            vessel.autopilot.mode = MODE_PROGRADE }        

        if (vessel.altitude_sealevel <= vessel.main_body.atmosphere_depth) {
            0.1 }
        
        else { 
            0.0 } }

}