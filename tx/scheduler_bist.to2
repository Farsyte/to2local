use { CONSOLE } from ksp::console
use { Vessel } from ksp::vessel
use { sleep, current_time } from ksp::game

use { Scheduler, NEVER } from fx::scheduler
use { Log } from fx::log

use { Bist, BistResult, report } from fx::bist

fn test_sch_init(bist: Bist, name: string, sch: Scheduler) -> BistResult = {
    bist.assert_true(name + " sch has initial NEVER task", sch.next.defined)?
    bist.assert_float(name + " never-task scheduled time", NEVER, NEVER, sch.next_when())?
    bist.assert_string(name + " never-task name", "never", sch.next_name())?

    bist.assert_false(name + " stage_pending initially should be false", sch.stage_pending())?
    sch.stage_request()
    bist.assert_true(name + " stage_request sets stage_pending to TRUE", sch.stage_pending())?
    sch.stage_request()
    bist.assert_true(name + " stage_request is idempotent", sch.stage_pending())?
    sch.stage_cancel()
    bist.assert_false(name + " stage_cancel sets stage_pending to FALSE", sch.stage_pending())?
    sch.stage_cancel()
    bist.assert_false(name + " stage_cancel is idempotent", sch.stage_pending())?

    bist.summary(name) }

fn test_sch_once(bist: Bist, name: string, sch: Scheduler) -> BistResult = {
    const t0 = current_time()
    sch.run_at(0.1, "once", fn() -> 0.0)

    bist.assert_float(name + " once-task scheduled time", t0+0.09, t0+0.11, sch.next_when())?
    bist.assert_string(name + " once-task name", "once", sch.next_name())?

    let t2 = sch.next_task()
    bist.assert_float(name + " once-task return value", 0.0, 0.0, t2())?

    sch.step()
    bist.assert_string(name + " once-task still pending", "once", sch.next_name())?

    sch.step()
    bist.assert_string(name + " once-task still pending", "once", sch.next_name())?

    sleep(0.15)

    sch.step()
    bist.assert_string(name + " once-task no longer pending", "never", sch.next_name())?

    bist.summary(name) }

fn test_sch_twice(bist: Bist, name: string, sch: Scheduler) -> BistResult = {
    const t0 = current_time()
    sch.run_at(0.10, "twice", fn() -> 0.1)

    bist.assert_float(name + " twice-task scheduled time", t0+0.09, t0+0.11, sch.next_when())?
    bist.assert_string(name + " twice-task name", "twice", sch.next_name())?

    let t2 = sch.next_task()
    bist.assert_float(name + " twice-task return value", 0.1, 0.1, t2())?

    let t2when = sch.next_when()

    sch.step()
    bist.assert_string(name + " twice-task still pending", "twice", sch.next_name())?
    bist.assert_float(name + " twice-task same schedule", t2when, t2when, sch.next_when())?

    sch.step()
    bist.assert_string(name + " twice-task still pending", "twice", sch.next_name())?
    bist.assert_float(name + " twice-task same schedule", t2when, t2when, sch.next_when())?

    sleep(0.11)
    let t3 = current_time()

    sch.step()
    bist.assert_string(name + " twice-task still pending", "twice", sch.next_name())?
    bist.assert_float(name + " twice-task second schedule", t3+0.00, t3+0.20, sch.next_when())?
    let t3when = sch.next_when()

    sch.step()
    bist.assert_string(name + " twice-task still pending", "twice", sch.next_name())?
    bist.assert_float(name + " twice-task third schedule", t3when, t3when, sch.next_when())?

    sch.next = sch.next.value.next
    bist.assert_string(name + " twice-task no longer pending", "never", sch.next_name())?

    bist.summary(name) }

fn test_sch_fizzbuzz(bist: Bist, name: string, sch: Scheduler) -> BistResult = {
    const log : Log = Log()

    sch.run_at(0.06, "fizz", fn() -> {
        if (log.lines() > 4) return 0.0
        log.add("fizz")
        0.06 })

    sch.run_at(0.10, "buzz", fn() -> {
        if (log.lines() > 4) return 0.0
        log.add("buzz")
        0.10 })

    sch.loop()

    bist.assert_string(name + " log entry 1 ", "fizz", log.pop().value)
    bist.assert_string(name + " log entry 2 ", "buzz", log.pop().value)
    bist.assert_string(name + " log entry 3 ", "fizz", log.pop().value)
    bist.assert_string(name + " log entry 4 ", "fizz", log.pop().value)
    bist.assert_string(name + " log entry 5 ", "buzz", log.pop().value)

    bist.summary(name) }

// provide the "fx::test_scheduler" public method that is called
// by this file, by all_bist, and by any mission scripts that
// want to assure SCHEDULER is not broken before they run.

const name : string = "scheduler"

pub fn test_scheduler(bist: Bist, vessel: Vessel) -> BistResult = {

    // Note: bist.assert_*()? calls (with a trailing ?)
    // indicate that if that case fails, do nothing more
    // in this set of cases, returning the failure upward.

    // A fresh scheduler will report that the next pending
    // task happens NEVER, and if we call it, it returns
    // a duration that will be reached NEVER.

    const sch = Scheduler(vessel)
    test_sch_init(bist, name + " init", sch)?
    test_sch_once(bist, name + " once", sch)?
    test_sch_twice(bist, name + " twice", sch)?
    test_sch_fizzbuzz(bist, name + " fizzbuzz", sch)?

    bist.summary(name) }

// proivide the "fx::test::scheduler_bist::main_flight" entry point
// that runs just the SCHEDULER tests.

pub fn main_flight(vessel: Vessel) -> Result<Unit, string> = {
    CONSOLE.print_line("testing " + name + " ...")

    const bist = Bist()
    const result = test_scheduler(bist, vessel)

    if (!result.success) {
        bist.add_fail_head(name, result.error)
            .add_fail_tail(name)
        bist.print() }
    report(result)? }
