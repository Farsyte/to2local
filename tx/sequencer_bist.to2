use { Vessel } from ksp::vessel
use { CONSOLE } from ksp::console
use { sleep, current_time } from ksp::game

use { Log } from fx::log
use { Sequencer } from fx::sequencer
use { Bist, BistResult, report } from fx::bist

fn test_seq_init(bist: Bist, name: string, seq: Sequencer) -> BistResult = {

    bist.assert_false(name + " stage_pending initially should be false", seq.stage_pending())?
    seq.stage_request()
    bist.assert_true(name + " stage_request sets stage_pending to TRUE", seq.stage_pending())?
    seq.stage_request()
    bist.assert_true(name + " stage_request is idempotent", seq.stage_pending())?
    seq.stage_cancel()
    bist.assert_false(name + " stage_cancel sets stage_pending to FALSE", seq.stage_pending())?
    seq.stage_cancel()
    bist.assert_false(name + " stage_cancel is idempotent", seq.stage_pending())?

    // initially, there are no tasks in the main sequence, and attempting to
    // execute the "next task" will return a zero indiating that the main
    // sequence runner task does not need to be called again.

    bist.assert_float(name + " step initial sequence indicates DONE",
        0.0, 0.0, seq.step())

    {   // check runtime of seq.loop() with empty list
        const t0 = current_time()
        seq.loop()
        const dt = current_time() - t0
        bist.assert_float(name + " seq.loop() of empty sequence returns promptly",
            0.0, 5.0, dt) }

    bist.summary(name) }

struct Visited(name: string, repeats : int = 0) {
    name : string = name
    repeats: int = repeats
    count : int = 0
    when : float = 0.0
}

impl Visited {
    sync fn visit(self) -> float = {
        self.count = self.count + 1
        self.when = current_time()
        if (self.count > self.repeats) -0.1 else 0.1 }
}

fn test_seq_once(bist: Bist, name: string, seq: Sequencer) -> BistResult = {
    // verify operation of sequencer executing three tasks, once each.

    const t0 : float = current_time()

    const task0 : Visited = Visited("task0")
    const task1 : Visited = Visited("task1")
    const task2 : Visited = Visited("task2")
    const task3 : Visited = Visited("task3")

    seq
        .next_seq(fn() -> task1.visit())
        .next_seq(fn() -> task2.visit())
        .next_seq(fn() -> task3.visit())
        .loop()

    task0.visit()

    bist.assert_float(name + " seq.loop() for test_seq_once ran in good time",
        0.0 + t0, 10.0 + t0, task0.when)

    bist.assert_int(name + " seq ran task1 exactly once",
        1, 1, task1.count)
    bist.assert_int(name + " seq ran task2 exactly once",
        1, 1, task2.count)
    bist.assert_int(name + " seq ran task3 exactly once",
        1, 1, task3.count)

    bist.assert_float(name + " seq ran task1 at the right time",
        0.0 + t0, 5.0 + t0, task1.when)
    bist.assert_float(name + " seq ran task2 at the right time",
        0.05 + task1.when, 0.15 + task1.when, task2.when)
    bist.assert_float(name + " seq ran task3 at the right time",
        0.05 + task2.when, 0.15 + task2.when, task3.when)

    bist.summary(name) }

fn test_seq_repeats(bist: Bist, name: string, seq: Sequencer) -> BistResult = {
    // verify operation of sequencer executing three tasks, once each.

    const t0 : float = current_time()

    const task0 : Visited = Visited("task0")
    const task1 : Visited = Visited("task1")
    const task2 : Visited = Visited("task2", 9)
    const task3 : Visited = Visited("task3")

    seq
        .next_seq(fn() -> task1.visit())
        .next_seq(fn() -> task2.visit())
        .next_seq(fn() -> task3.visit())
        .loop()

    task0.visit()

    bist.assert_float(name + " seq.loop() for test_seq_once ran in good time",
        0.0 + t0, 10.0 + t0, task0.when)

    bist.assert_int(name + " seq ran task1 exactly once",
        1, 1, task1.count)
    bist.assert_int(name + " seq ran task2 exactly ten times",
        10, 10, task2.count)
    bist.assert_int(name + " seq ran task3 exactly once",
        1, 1, task3.count)

    bist.assert_float(name + " seq ran task1 at the right time",
        0.0 + t0, 5.0 + t0, task1.when)
    bist.assert_float(name + " seq ran task2 for the last time at the right time",
        1.05 + task1.when, 1.25 + task1.when, task2.when)
    bist.assert_float(name + " seq ran task3 at the right time",
        0.05 + task2.when, 0.15 + task2.when, task3.when)

     bist.summary(name) }

// provide the "fx::test_sequencer" public method that is called
// by this file, by all_bist, and by any mission scripts that
// want to assure SEQUENCER is not broken before they run.

const name : string = "sequencer"

pub fn test_sequencer(bist: Bist, vessel: Vessel) -> BistResult = {

    const seq = Sequencer(vessel)

    test_seq_init(bist, name + " init", seq)?
    test_seq_once(bist, name + " once", seq)?
    test_seq_repeats(bist, name + " ten times", seq)?

    bist.summary(name) }

// proivide the "fx::test::sequencer_bist::main_flight" entry point
// that runs just the SEQUENCER tests.

pub fn main_flight(vessel: Vessel) -> Result<Unit, string> = {
    CONSOLE.print_line("testing " + name + " ...")

    const bist = Bist()
    const result = test_sequencer(bist, vessel)

    if (!result.success) {
        bist.add_fail_head(name, result.error)
            .add_fail_tail(name)
        bist.print() }
    report(result)? }
