use { Vessel } from ksp::vessel
use { CONSOLE } from ksp::console

pub struct Sequencer() {
    next: NodeRef = no_node
    last: NodeRef = no_node
    step: int = 0
}

type NodeRef = Option<Node>

struct Node(task: fn() -> bool) {
    next: NodeRef = no_node
    task: fn() -> bool = task
}

// oddity in KnotrolSystem2 0.2.2.0
// this initializer was not working:
//    next: NodeRef = None()
const no_node : NodeRef = None()

impl Sequencer {
    fn add(self, task: fn() -> bool) -> Unit = {
        const nref : NodeRef = Some(Node(task))
        const last : NodeRef = self.last
        if (!self.next.defined)
            self.next = nref
        if (self.last.defined)
            self.last.value.next = nref
        self.last = nref
    }
    fn step(self) -> bool = {
        if (self.next.defined) {
            const n = self.next.value
            if (!n.task()) {
                self.next = n.next
            }
        }
        self.next.defined
    }
}