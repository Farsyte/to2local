// LOG: a log of text lines that can be reviewed.

use { CONSOLE } from ksp::console

// The log is stored as a single-linked list of nodes
// called LogLine, which contain a reference to the
// string for this line, and a reference to the next
// LogLine node (which might be None).

struct LogLine(text: string) {
    text : string = text
    next : LogLineRef = NO_LINE
}

// References are just an Option<> that could
// be None, or could be a LogLine. This actually
// stores a pointer, not a copy, of the LogLine.

type LogLineRef = Option<LogLine>

// KontrolSystem2 type inferrance fails on this kind of initialization:
//     next : Option<LogLine> = None()

const NO_LINE : LogLineRef = None()

// This is the log itself. Store a reference to the first
// line so we can observe the log, and to the last line
// so we can append to it. Also store the length so we can
// make decisions based on the size.

pub struct Log {
    top : Option<LogLine> = NO_LINE
    end : Option<LogLine> = NO_LINE
    count : int = 0
}

impl Log {
    sync fn lines(self) -> int = {
        self.count }

    sync fn clear(self) -> Log = {
        self.top = NO_LINE
        self.end = NO_LINE
        self.count = 0
        self }

    sync fn empty(self) -> bool = {
        !self.top.defined }

    sync fn add(self, text: string) -> Log = {
        // used by BIST. must be tested outside the BIST framework.
        const bllr : LogLineRef = Some(LogLine(text))
        if (self.end.defined) self.end.value.next = bllr
        if (!self.top.defined) self.top = bllr
        self.end = bllr
        self.count += 1
        self }

    sync fn pop(self) -> Option<string> = {
        // used by BIST. must be tested outside the BIST framework.
        if (self.empty()) {
            None() }

        else {
            const ll = self.top.value
            self.top = ll.next
            self.count -= 1
            Some(ll.text) } }

    fn print(self) -> Log = {
        // the effect of this function is not something that can
        // be tested during automation -- at best, we can exercise
        // it and ask the flight engineer to confirm operation.
        let llr = self.top
        while (llr.defined) {
            const ll : LogLine = llr.value
            CONSOLE.print_line(ll.text)
            llr = ll.next }

        self }

}
