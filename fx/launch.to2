use { ceiling } from core::math

use { CONSOLE } from ksp::console
use { current_time } from ksp::game
use { MODE_STABILITYASSIST } from ksp::vessel

use { Common } from fx::common

pub struct Launch(common: Common) {
    common : Common = common
    mode : int = 0
    mode_ut : float = 0.0

    countdown : int = 0

    launch_height : float = 36.0        // estimated from video footage
    liftoff_done_alt : float = 0.0

    debug_print_ut : float = 0.0

    launch_thrust : float = 0.0
}

impl Launch {

    sync fn launchtime(self) -> float = {
        const common = self.common

        // Open for expansion:
        //
        // When initating support for missions that constrain the
        // launch window (such as waiting until we are within the
        // orbital plane of a destination), add code here to examine
        // data in common to guide selection of launch time.
        //
        // Absent any constraints, launch in 3 seconds.

        common.t0 = current_time() + 10.0

        0.0
    }

    sync fn countdown(self) -> float = {
        const common = self.common

        const t = current_time()

        if (self.mode != 1) {
            self.mode = 1
            self.mode_ut = t
        }

        const dt = common.t0 - current_time()
        const countdown = ceiling(dt).to_int
        if (self.countdown != countdown) {
            self.countdown = countdown
            if (0 < countdown && countdown < 11)
                CONSOLE.print_line("countdown: T-" + countdown.to_string())
        }

        if (dt > 0) {
            0.1
        } else {
            0.0
        }
    }

    sync fn liftoff(self) -> float = {
        const common = self.common
        const vessel = common.vessel

        const t = current_time()

        if (self.mode != 2) {
            self.mode = 2
            self.mode_ut = t
            self.liftoff_done_alt = vessel.altitude_sealevel + self.launch_height
            common.t0 = t       // this is the REAL T-0 universal time.
            common.t_mgr.throttle = 1.0
            vessel.autopilot.enabled = true
            vessel.autopilot.mode = MODE_STABILITYASSIST
        }

        if (vessel.altitude_sealevel < self.liftoff_done_alt) {
            0.1
        } else {
            0.0
        }
    }

    sync fn manual(self) -> float = {
        const common = self.common
        const vessel = common.vessel

        const t = current_time()

        if (self.mode != 3) {
            self.mode = 3
            self.mode_ut = t

            const body = vessel.main_body
            const mu = body.grav_parameter
            const r0 = body.radius + vessel.altitude_sealevel
            const r2 = r0 * r0
            const m = vessel.mass

            // have the throttle at 100% while waiting for the
            // flight engineer to hit the GO button.

            common.t_mgr.throttle = 1.0

            self.launch_thrust = mu * m / r2

            CONSOLE.print_line("awaiting launch")
        }

        if (vessel.available_thrust <= self.launch_thrust) {
            0.1
        } else {
            0.0
        }
    }

}