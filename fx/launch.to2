use { ceiling } from core::math

use { CONSOLE } from ksp::console
use { current_time } from ksp::game
use { MODE_STABILITYASSIST } from ksp::vessel

use { Common } from fx::common

pub struct Launch(common: Common) {
    common : Common = common
    mode : int = 0
    mode_met : float = 0.0

    countdown : int = 0

    launch_height : float = 36.0        // estimated from video footage
    liftoff_done_alt : float = 0.0
}

impl Launch {

    sync fn launchtime(self) -> bool = {
        const common = self.common

        // Open for expansion:
        //
        // When initating support for missions that constrain the
        // launch window (such as waiting until we are within the
        // orbital plane of a destination), add code here to examine
        // data in common to guide selection of launch time.
        //
        // Absent any constraints, launch in 3 seconds.

        common.t0 = current_time() + 10.0

        false
    }

    sync fn countdown(self) -> bool = {
        const common = self.common
        if (self.mode != 1) {
            self.mode = 1
            self.mode_met = common.met()
            CONSOLE.print_line("start countdown")
        }

        const dt = common.t0 - current_time()
        const countdown = ceiling(dt).to_int
        if (self.countdown != countdown) {
            self.countdown = countdown
            if (0 < countdown && countdown < 11)
                CONSOLE.print_line("countdown: T-" + countdown.to_string())
        }
        dt > 0
    }

    sync fn liftoff(self) -> bool = {
        const common = self.common
        const vessel = common.vessel
        if (self.mode != 2) {
            self.mode = 2
            self.mode_met = common.met()
            self.liftoff_done_alt = vessel.altitude_sealevel + self.launch_height
            common.t_mgr.throttle = 1.0
            vessel.autopilot.enabled = true
            vessel.autopilot.mode = MODE_STABILITYASSIST
            CONSOLE.print_line("start liftoff")
        }
        vessel.altitude_sealevel < self.liftoff_done_alt
    }

}