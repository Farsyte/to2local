use { Vessel } from ksp::vessel
use { CONSOLE } from ksp::console

use { yield, sleep, current_time } from ksp::game

pub struct Scheduler(vessel: Vessel) {
    vessel : Vessel = vessel

    pend_stage : bool = false

    next : Option<SchTask> = make_last_sch_task()
    name : string = ""
}

// sch_insert(sch, node): insert a node into the schedule.
// only sch_insert and sch_remove care that the schedule
// is a singlely-linked list in nondescending scheduled time
// with a sentinal task at the end whose WHEN key is NEVER.
sync fn sch_insert(sch: Scheduler, node: SchTask) -> Unit = {

    if (!sch.next.defined) {
        // safety net (should never happen)
        sch.next = Some(node)
        return }

    if (node.when < sch.next.value.when) {
        // expected common case: inserting at the head of the list
        node.next = sch.next
        sch.next = Some(node)
        return }

    // insert into the list after all nodes
    // whose scheduled times are earlier or the same.
    // TODO optimize for guaranteed presence of sentinel task
    let t1 = sch.next.value
    while (t1.next.defined) {
        let t2 = t1.next.value
        if (node.when < t2.when) {
            break }
        t1 = t2 }

    node.next = t1.next
    t1.next = Some(node) }

sync fn sch_remove(sch: Scheduler) -> Option<SchTask> = {
    // TODO prove we never remove the sentinel task, or prevent it.
    const result = sch.next
    if (result.defined)
        sch.next = result.value.next
    result }

impl Scheduler {
    // Fundamental "vessel.staging.next()" call is ASYNC.
    // provide simple sync fn stage_{pending,request,cancel}
    // to indicate when staging is (or is not) desired.
    sync fn stage_pending(self) -> bool = { self.pend_stage }
    sync fn stage_request(self) -> Unit = { self.pend_stage = true }
    sync fn stage_cancel(self) -> Unit = { self.pend_stage = false }

    // simple sync fn next_{when,name,task} to examine the next task.
    sync fn next_when(self) -> float = { self.next.value.when }
    sync fn next_name(self) -> string = { self.next.value.name }
    sync fn next_task(self) -> fn() -> float = { self.next.value.task }

    // run_at(eta,name,task): in no less than eta seconds, run task.
    // name string stored for debug, status monitor, and logging (TBD).
    sync fn run_at(self, eta: float, name: string, task: fn() -> float) -> Unit = {
        sch_insert(self, SchTask(eta, name, task)) }

    sync fn step(self) -> Unit = {

        // This check also assures that we will never attempt to
        // remove an existing sentinel task (and thus never attempt
        // to re-insert it after removal).
        if (current_time() < self.next_when()) {
            return }

        const r : Option<SchTask> = sch_remove(self)
        if (!r.defined) {
            return }

        if (self.name != r.value.name) {
            self.name = r.value.name }

        // time may advance during this call.
        let dt = r.value.task()

        if (dt > 0.0) {
            r.value.when = current_time() + dt
            sch_insert(self, r.value) } }

    fn async(self) -> Unit = {

        if (self.stage_pending()) {
            // The call to KSP to trigger staging is ASYNC.
            self.vessel.staging.next()
            self.stage_cancel() } }

    fn loop(self) -> Unit = {
        while (self.next_when() < NEVER) {
            self.step()
            self.async()

            // open for extension: we could decide to sleep
            // for a bit, if it is important to conserve
            // host CPU cycles. Would not want to sleep longer
            // than the ETA of the next task, and probably would
            // not want to sleep for a long time in any case.

            yield() } }
}

pub const NEVER : float = 1.0 / 0.0         // yes, Infinity.

// KontrolSystem2 type inferrance fails on this kind of initialization:
//    next: Option<SchTask> = None()
const noSchTaskRef : Option<SchTask> = None()

struct SchTask(eta: float, name: string, task: fn() -> float) {
    next : Option<SchTask> = noSchTaskRef
    when : float = eta + current_time()
    name : string = name
    task : fn() -> float = task }

sync fn make_last_sch_task() -> Option<SchTask> = {
    SchTask(NEVER, "never", fn() -> NEVER) }
