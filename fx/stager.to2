use { CONSOLE } from ksp::console
use { Common } from fx::common
pub struct Stager(common: Common) {
    common : Common = common
}
impl Stager {
    fn count_flameout(self) -> int = {
        const common = self.common
        common.vessel.staging
            .parts_in_stage(common.vessel.staging.current)
            .filter(fn(part) -> part.is_engine)
            .filter(fn(part) -> part.engine_module.defined)
            .map(fn(part) -> part.engine_module.value)
            .filter(fn(eng) -> eng.is_flameout)
            .length
    }

    fn stage_needed(self) -> bool = {
        const common = self.common
        const vessel = common.vessel
        const staging = vessel.staging

        const s = staging.current
        if (2 > staging.current) {
            return false
        }
        const met = common.met()
        if (0.0 > met) {
            return false
        }
        const t = vessel.available_thrust
        if (0.0 == t) {
            return true
        }
        const foc = self.count_flameout()
        if (0 != foc) {
            return true
        }
        return false
    }

    fn maybe_stage(self) -> Unit = {
        if (self.stage_needed())
            self.common.vessel.staging.next()
    }
}